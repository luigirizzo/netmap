# See http://wiki.archlinux.org/index.php/VCS_PKGBUILD_Guidelines
# for more information on packaging from GIT sources.

# Maintainer: Vincenzo Maffione <v.maffione@gmail.com>
pkgname=netmap
pkgver=3.16
pkgrel=1
pkgdesc="Netmap is a framework for high speed network packet I/O."
arch=('any')
url="http://info.iet.unipi.it/~luigi/netmap"
license=('BSD')
groups=()
depends=('linux' 'glibc')
makedepends=('git' 'sed' 'gzip' 'linux-headers' 'abs' 'pacman')
provides=()
conflicts=()
replaces=()
backup=()
options=()
install="netmap.install"
source=("netmap.install")
noextract=()
md5sums=("047aa5adec4c52ddbf86d12dbf300f71")

_gitroot="https://v.maffione@code.google.com/p/netmap/"
_gitname="netmap"

build() {
    # Download the latest netmap code from the public repository
    cd "$srcdir"
    msg "Connecting to GIT server...."
    if [[ -d "$_gitname" ]]; then
        cd "$_gitname" && git pull origin
        msg "The local files are updated."
    else
        git clone "$_gitroot" "$_gitname"
        cd "$srcdir/$_gitname"
        git branch next origin/next
        git checkout next
    fi
    msg "GIT checkout done or server timeout"

    # Download kernel sources using ABS, checking that the version is the
    # same as the running kernel
    msg "Downloading kernel sources..."
    mkdir -p $srcdir/abs
    cd $srcdir/abs
    ABSROOT=. abs core/linux
    cd $srcdir/abs/core/linux
    grep "pkgver[ ]*=" PKGBUILD > .ksver
    KSVER=$(sed 's|pkgver[ ]*=[ ]*||g' .ksver)
    rm .ksver
    RKVER=$(uname -r | sed 's|-.*||g')
    if [ "$KSVER" != "$RKVER" ]; then
        msg "Kernel sources version ($KSVER) differs from running kernel version ($RKVER): Cannot continue"
        return 1
    fi
    KMAJVER=$(echo "$KSVER" | sed 's|\.[0-9]\+$||g')
    makepkg --nobuild
    msg "Kernel sources are ready"

    # Build the netmap kernel module and all modified drivers, using the
    # kernel sources downloaded in the previous steps to copy the NIC
    # drivers. Note however that the kernel modules are built against the
    # running kernel, and not against the downloaded sources.
    msg "Starting to build netmap"
    cd "$srcdir/$_gitname/LINUX"
    unset SRC
    unset KSRC
    unset NODRIVERS
    make SRC=$srcdir/abs/core/linux/src/linux-$KMAJVER || return 1
    # Build pkt-gen and vale-ctl
    cd "$srcdir/$_gitname/examples"
    make clean  # amend for existing .o
    make pkt-gen vale-ctl || return 1
    msg "Build complete"
}

package() {
    # Compute the version numbers of the running kernel
    KVER1=$(uname -r)
    KVER2=$(uname -r | sed 's/\.[0-9]\+-[0-9]\+//')

    # Install the netmap module into the extramodules-VERSION directory
    mkdir -p "$pkgdir/lib/modules/extramodules-${KVER2}"
    cp "$srcdir/$_gitname/LINUX/netmap_lin.ko" "$pkgdir/lib/modules/extramodules-${KVER2}"

    # Install pkt-gen and valectl into /usr/bin
    mkdir -p "$pkgdir/usr/bin"
    cp "$srcdir/$_gitname/examples/pkt-gen" "$pkgdir/usr/bin"
    cp "$srcdir/$_gitname/examples/vale-ctl" "$pkgdir/usr/bin"

    # Install the netmap public headers
    mkdir -p "$pkgdir/usr/include/net"
    cp "$srcdir/$_gitname/sys/net/netmap.h" "$srcdir/$_gitname/sys/net/netmap_user.h" "$pkgdir/usr/include/net"

    # Install the netmap man page
    mkdir -p "$pkgdir/usr/share/man/man4"
    cp "$srcdir/$_gitname/share/man/man4/netmap.4" "$pkgdir/usr/share/man/man4"
    gzip "$pkgdir/usr/share/man/man4/netmap.4"

    #Find and install the modified NIC drivers
    cd "$srcdir/$_gitname/LINUX"
    DRIVERS=$(find . -name "*.ko" -and ! -name "netmap_lin.ko")
    if [ -n "$DRIVERS" ]; then
        mkdir -p "$pkgdir/lib/modules/extramodules-${KVER2}/netmap-drivers"
        cp --parent $DRIVERS "$pkgdir/lib/modules/extramodules-${KVER2}/netmap-drivers"
        cd "$pkgdir/lib/modules/extramodules-${KVER2}/netmap-drivers"
        find . -name "*.ko" -exec sh -c "mv {} \$(echo {} | sed 's|\.ko|_netmap\.ko|g')" \;
    fi
}

# vim:set ts=2 sw=2 et:
